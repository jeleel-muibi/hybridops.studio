# No provider block needed - it's generated by root.hcl

# VLAN Zone for SDN
resource "proxmox_virtual_environment_sdn_zone_vlan" "main" {
  id     = var.zone_name
  bridge = var.uplink_bridge
  mtu    = 1500
  ipam   = "pve"
}

# Virtual Networks (VNets)
resource "proxmox_virtual_environment_sdn_vnet" "vnets" {
  for_each = var.vnets

  id    = each.key
  zone  = proxmox_virtual_environment_sdn_zone_vlan.main.id
  tag   = each.value.vlan_id
  alias = coalesce(each.value.comment, "${var.environment}-${each.key}")

  depends_on = [proxmox_virtual_environment_sdn_zone_vlan.main]
}

# Subnets for each VNet
resource "proxmox_virtual_environment_sdn_subnet" "subnets" {
  for_each = var.vnets

  vnet    = proxmox_virtual_environment_sdn_vnet.vnets[each.key].id
  cidr    = each.value.cidr
  gateway = each.value.gateway
  snat    = true

  depends_on = [proxmox_virtual_environment_sdn_vnet.vnets]
}
